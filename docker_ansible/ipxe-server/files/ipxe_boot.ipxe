#!ipxe

chain http://192.168.1.195/cfg/${mac}.ipxe ||

:start
set menu-timeout 10000
set default-option localboot

menu PXE Boot Menu (auto-boot in 10s)
item --key d debian        Install Debian 13
item --key u ubuntu2404    Install Ubuntu24.04 LTS(min 2G RAM)
item --key m memtest       Run Memtest86+
item --key s systemrescue  SystemRescue(min 3G RAM)
item --key h localboot     Boot from local disk
item --key r shell         iPXE Shell
item --key 0 reboot        Reboot
choose --timeout ${menu-timeout} --default ${default-option} selected || goto cancel

goto ${selected}

:debian
echo Booting Debian 13 Installer...
kernel http://192.168.1.195/debian13/linux auto=true priority=critical interface=auto ip=dhcp url=http://192.168.1.195/debian13.cfg
initrd http://192.168.1.195/debian13/initrd.gz
boot

:ubuntu2404
echo Booting Ubuntu 24.04.3 LTS Autoinstall via NFS...
set server-ip 192.168.1.195
set base-url http://${server-ip}/ubuntu24.04-lts/casper
kernel ${base-url}/vmlinuz initrd=initrd ip=dhcp boot=casper netboot=nfs nfsroot=${server-ip}:/srv/httpboot/ubuntu24.04-lts autoinstall noprompt ds=nocloud-net;s=http://${server-ip}/ubuntu24.04-lts/
initrd ${base-url}/initrd
boot

:memtest
echo Booting Memtest86+(64-bit)...
kernel http://192.168.1.195/memtest64.bin
boot

:systemrescue
echo Booting SystemRescue...
kernel http://192.168.1.195/systemrescue/sysresccd/boot/x86_64/vmlinuz \
    archiso_http_srv=http://192.168.1.195/systemrescue/ \
    archisobasedir=sysresccd \
    ip=dhcp
initrd http://192.168.1.195/systemrescue/sysresccd/boot/x86_64/sysresccd.img
boot

:localboot
echo Booting from local drive...
# Check if we in the UEFI mode (In the iPXE ${platform} is 'efi' for UEFI or 'pcbios' for Legacy
iseq ${platform} efi && goto boot_efi || goto boot_bios

:boot_bios
sanboot --no-describe --drive 0x80 || goto failed

:boot_efi
# In the UEFI iPXE can't set boot from disk.
# 'exit 0' return control for UEFI (Boot Manager). Disk should be in the boot list (even second).
exit 0

:failed
echo Local boot failed. Starting shell...
shell


:shell
shell

:reboot
reboot

:cancel
echo Boot cancelled by user
exit

