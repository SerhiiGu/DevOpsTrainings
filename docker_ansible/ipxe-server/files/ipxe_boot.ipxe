#!ipxe

# 10 seconds (in msec)
set menu-timeout 10000
set default-option debian

:start
menu PXE Boot Menu (auto-boot in 10s)
item --key d debian        Install Debian 13 (Default)
item --key m memtest       Run Memtest86+
item --key s systemrescue  SystemRescue(minimum 3G RAM)
item --key h localboot     Boot from local disk
item --key r shell         iPXE Shell
item --key 0 reboot        Reboot
choose --timeout ${menu-timeout} --default ${default-option} selected || goto cancel

goto ${selected}

:debian
echo Booting Debian 13 Installer...
kernel http://192.168.1.195/debian13/linux auto=true priority=critical interface=auto ip=dhcp url=http://192.168.1.195/debian13.cfg
initrd http://192.168.1.195/debian13/initrd.gz
boot

:memtest
echo Booting Memtest86+(64-bit)...
kernel http://192.168.1.195/memtest64.bin
boot

:systemrescue
echo Booting SystemRescue...
kernel http://192.168.1.195/systemrescue/sysresccd/boot/x86_64/vmlinuz \
    archiso_http_srv=http://192.168.1.195/systemrescue/ \
    archisobasedir=sysresccd \
    ip=dhcp
initrd http://192.168.1.195/systemrescue/sysresccd/boot/x86_64/sysresccd.img
boot

:localboot
echo Booting from local drive...
exit

:shell
shell

:reboot
reboot

:cancel
echo Boot cancelled by user
exit

